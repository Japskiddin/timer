apply plugin: 'com.android.application'

android {
  compileSdkVersion 27
  signingConfigs {
    release {
      if (project.hasProperty("Keys.repo")) {
        def projectPropsFile = file(project.property("Keys.repo") + "/timer.properties")
        if (projectPropsFile.exists()) {
          Properties props = new Properties()
          props.load(new FileInputStream(projectPropsFile))

          storeFile file(file(project.property("Keys.repo") + props['RELEASE_STORE_FILE']))
          storePassword props['RELEASE_STORE_PASS']
          keyAlias props['RELEASE_ALIAS']
          keyPassword props['RELEASE_KEY_PASS']
        }
      } else {
        println "======================================================="
        println "[ERROR] - Please configure release-compilation environment - e.g. in ~/.signing  directory"
        println "======================================================="
      }
    }
  }
  defaultConfig {
    applicationId "com.rusdelphi.timer"
    minSdkVersion 14
    targetSdkVersion 27
    versionCode 9
    versionName "1.2.3"
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
    }
  }
  flavorDimensions "color"
  productFlavors {
    // Define separate dev and prod flavors
    dev21 {
      minSdkVersion 21
      dimension "color"
    }
    prod {
      // The actual minSdkVersion for the production app
      minSdkVersion 15
      dimension "color"
    }
  }
}
ext {
  supportLibrary = "27.0.2"
  playServices = "11.8.0"
}
dependencies {
  compile "com.google.android.gms:play-services-ads:${playServices}"
  compile "com.android.support:appcompat-v7:${supportLibrary}"
  compile "com.android.support:design:${supportLibrary}"
  compile "com.android.support:support-v4:${supportLibrary}"
  compile "com.android.support:cardview-v7:${supportLibrary}"
  compile "com.android.support:recyclerview-v7:${supportLibrary}"
  compile 'com.google.code.gson:gson:2.8.2'
}
